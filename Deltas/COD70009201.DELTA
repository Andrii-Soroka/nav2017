OBJECT Codeunit 70009201 G/L Source Name Mgt
{
  OBJECT-PROPERTIES
  {
    Date=25.02.17;
    Time=09:18:48;
    Version List=GLSN10.0;
  }
  PROPERTIES
  {
    Permissions=TableData 70009200=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Window@70013401 : Dialog;
      SourceType@70013400 : ' ,Customer,Vendor,Bank Account,Fixed Asset';
      ProcessInfo@70013402 : TextConst 'ENU=Populating G/L Source Names...';
      RequiredPermissionMissingErr@1000000000 : TextConst 'ENU=Required permissions for table "%1" is missing.  Can''t refresh lookup data.';
      ProcessCompleted@70009200 : TextConst 'ENU=G/L Source Names lookup table update finished';

    PROCEDURE AddSource@1035(SourceType@1020 : Option;SourceNo@1021 : Code[20];SourceName@1022 : Text[50]);
    VAR
      GLSourceName@1023 : Record 70009200;
    BEGIN
      WITH GLSourceName DO BEGIN
        IF NOT WRITEPERMISSION THEN EXIT;
        IF GET(SourceType,SourceNo) THEN
          UpdateSource(SourceType,SourceNo,SourceName)
        ELSE BEGIN
          INIT;
          "Source Type" := SourceType;
          "Source No." := SourceNo;
          "Source Name" := SourceName;
          INSERT;
        END;
      END;
    END;

    PROCEDURE RemoveSource@1036(SourceType@1022 : Option;SourceNo@1021 : Code[20]);
    VAR
      GLSourceName@1023 : Record 70009200;
    BEGIN
      WITH GLSourceName DO BEGIN
        IF NOT WRITEPERMISSION THEN EXIT;
        IF GET(SourceType,SourceNo) THEN
          DELETE;
      END;
    END;

    PROCEDURE UpdateSource@1037(SourceType@1022 : Option;SourceNo@1021 : Code[20];SourceName@1020 : Text[50]);
    VAR
      GLSourceName@1023 : Record 70009200;
    BEGIN
      WITH GLSourceName DO BEGIN
        IF NOT WRITEPERMISSION THEN EXIT;
        IF GET(SourceType,SourceNo) THEN BEGIN
          "Source Type" := SourceType;
          "Source No." := SourceNo;
          "Source Name" := SourceName;
          MODIFY;
        END ELSE
          AddSource(SourceType,SourceNo,SourceName);
      END;
    END;

    PROCEDURE Refresh@1000000000(HideMessage@70009200 : Boolean);
    VAR
      GLSourceName@1000000004 : Record 70009200;
      Customer@1000000003 : Record 18;
      Vendor@1000000002 : Record 23;
      BankAccount@1000000001 : Record 270;
      FixedAsset@1000000000 : Record 5600;
    BEGIN
      WITH GLSourceName DO
        IF NOT WRITEPERMISSION THEN
          ERROR(RequiredPermissionMissingErr);
      WITH Customer DO
        IF NOT READPERMISSION THEN
          ERROR(RequiredPermissionMissingErr);
      WITH Vendor DO
        IF NOT READPERMISSION THEN
          ERROR(RequiredPermissionMissingErr);
      WITH BankAccount DO
        IF NOT READPERMISSION THEN
          ERROR(RequiredPermissionMissingErr);
      WITH FixedAsset DO
        IF NOT READPERMISSION THEN
          ERROR(RequiredPermissionMissingErr);
      GLSourceName.DELETEALL;
      PopulateSourceTable;
      IF NOT HideMessage THEN
        MESSAGE(ProcessCompleted);
    END;

    PROCEDURE PopulateSourceTable@1042();
    VAR
      GLSourceName@1020 : Record 70009200;
      Customer@1021 : Record 18;
      Vendor@1022 : Record 23;
      BankAccount@1023 : Record 270;
      FixedAsset@1024 : Record 5600;
    BEGIN
      IF GLSourceName.WRITEPERMISSION AND
        Customer.READPERMISSION AND
        Vendor.READPERMISSION AND
        BankAccount.READPERMISSION AND
        FixedAsset.READPERMISSION
      THEN BEGIN
        IF NOT GLSourceName.ISEMPTY THEN EXIT;
        IF Customer.ISEMPTY AND Vendor.ISEMPTY AND BankAccount.ISEMPTY AND FixedAsset.ISEMPTY THEN EXIT;
        Window.OPEN(ProcessInfo);
        AddSourceTable(SourceType::Customer,Customer);
        AddSourceTable(SourceType::Vendor,Vendor);
        AddSourceTable(SourceType::"Bank Account",BankAccount);
        AddSourceTable(SourceType::"Fixed Asset",FixedAsset);
        Window.CLOSE;
      END;
    END;

    PROCEDURE AddSourceTable@1044(SourceType@1021 : Option;RecVariant@1020 : Variant);
    VAR
      RecRef@1022 : RecordRef;
      NoField@1024 : FieldRef;
      NameField@1023 : FieldRef;
    BEGIN
      WITH RecRef DO BEGIN
        GETTABLE(RecVariant);
        IF FINDSET THEN REPEAT
          NoField := FIELD(1);
          NameField := FIELD(2);
          AddSource(SourceType,FORMAT(NoField.VALUE),FORMAT(NameField.VALUE));
        UNTIL NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

