OBJECT Codeunit 70009215 G/L Source Name Assisted Setup
{
  OBJECT-PROPERTIES
  {
    Date=08.02.17;
    Time=18:38:30;
    Version List=GLSN10.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Setup@1000000000 : Record 70009201;
      GLSourceNamesTxt@1000000001 : TextConst 'ENU=Set up G/L Source Names';
      HelpResource@1000000002 : Record 70009202;

    [EventSubscriber(Table,1808,OnRegisterAssistedSetup,"",Skip)]
    LOCAL PROCEDURE OnRegisterAssistedSetup@1(VAR TempAggregatedAssistedSetup@70009200 : TEMPORARY Record 1808);
    BEGIN
      IF NOT Setup.WRITEPERMISSION THEN EXIT;
      IF NOT HelpResource.WRITEPERMISSION THEN EXIT;
      InitializeSetup;
      AddToAssistedSetup(TempAggregatedAssistedSetup);
    END;

    [EventSubscriber(Table,1808,OnUpdateAssistedSetupStatus,"",Skip)]
    LOCAL PROCEDURE OnUpdateAssistedSetupStatus@2(VAR TempAggregatedAssistedSetup@70009200 : TEMPORARY Record 1808);
    BEGIN
      Setup.GET;
      WITH TempAggregatedAssistedSetup DO
        SetStatus(TempAggregatedAssistedSetup,PAGE::"G/L Source Name Setup Wizard",Setup.Status);
    END;

    LOCAL PROCEDURE InitializeSetup@4();
    BEGIN
      WITH Setup DO
        IF ISEMPTY THEN BEGIN
          INIT;
          INSERT;
        END ELSE
          GET;

      WITH HelpResource DO
        InitializeResources;
    END;

    LOCAL PROCEDURE AddToAssistedSetup@6(VAR TempAggregatedAssistedSetup@70009200 : TEMPORARY Record 1808);
    VAR
      TempBlob@70009201 : Record 99008535;
      GLSourceNameIcon@70009203 : Codeunit 70009232;
      InStr@70009202 : InStream;
    BEGIN
      WITH TempAggregatedAssistedSetup DO BEGIN
        GLSourceNameIcon.GetIcon(TempBlob);
        TempBlob.Blob.CREATEINSTREAM(InStr);
        InsertAssistedSetupIcon(HelpResource.Get240PXIconCode,InStr);

        AddExtensionAssistedSetup(
          PAGE::"G/L Source Name Setup Wizard",
          GLSourceNamesTxt,
          TRUE,
          Setup.RECORDID,
          Setup.Status,
          HelpResource.Get240PXIconCode);
      END;
    END;

    BEGIN
    END.
  }
}

